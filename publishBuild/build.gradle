/**
 * Build Script for publishing to maven.
 */

plugins {
    id 'java'
    id 'maven-publish'
    id 'signing'
}

group = project.getRootProject().group
version= project.getRootProject().version

apply plugin: 'maven-publish'
apply plugin: 'signing'

publishing {
    repositories {
        maven {
            url = "https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/"
            credentials {
                // Place this two variable with the correct values in your
                // <Gradle-User-Home>/gradle.properties
                //  ossrhUsername=ID
                //  ossrhPassword=TOKEN
                //
                username = findProperty("ossrhUsername")
                password = findProperty("ossrhPassword")
            }
        }
    }
    publications {
        jSVGIcon(MavenPublication) {

            artifacts = [project(':minimalBuild').jar, project(':minimalBuild').sourcesJar, project(':minimalBuild').javadocJar, project(':minimalBuild').examplesJar, project(':allFeaturesBuild').jar]

            pom {
                name = 'jSVGIcon'
                artifactId = 'jSVGIcon'
                packaging = 'jar'
                description = 'Draw icons and simple graphics from SVG that look great even on high-resolution screens.'
                url = 'https://github.com/BWeng20/jSVGIcon'
                licenses {
                    license {
                        name = 'unlicense'
                        url = 'https://unlicense.org'
                    }
                }
                developers {
                    developer {
                        id = 'bweng20'
                        name = 'Bernd Wengenroth'
                        email = 'Bernd.Wengenroth@gmail.com'
                    }
                }
                scm {
                    connection = 'https://github.com:BWeng20/jSVGIcon.git'
                    developerConnection = 'https://github.com:BWeng20/jSVGIcon.git'
                    url = 'https://github.com/BWeng20/jSVGIcon'
                }
            }
        }
        relocation(MavenPublication) {
            pom {
                name = 'jSVG'
                groupId = "io.github.bweng20"
                artifactId = "jSVG"
                version = "1.6"
                url = 'https://github.com/BWeng20/jSVGIcon'
                description = 'Draw icons and simple graphics from SVG that look great even on high-resolution screens.'
                licenses {
                    license {
                        name = 'unlicense'
                        url = 'https://unlicense.org'
                    }
                }
                developers {
                    developer {
                        id = 'bweng20'
                        name = 'Bernd Wengenroth'
                        email = 'Bernd.Wengenroth@gmail.com'
                    }
                }
                scm {
                    connection = 'https://github.com:BWeng20/jSVGIcon.git'
                    developerConnection = 'https://github.com:BWeng20/jSVGIcon.git'
                    url = 'https://github.com/BWeng20/jSVGIcon'
                }
                distributionManagement {
                    relocation {
                        groupId = "io.github.bweng20"
                        artifactId = "jSVGIcon"
                        version = "1.6"
                        message = "Relocated due to name conflict"
                    }
                }
            }
        }
    }
}

signing {
    sign publishing.publications.jSVGIcon
    sign publishing.publications.relocation
}